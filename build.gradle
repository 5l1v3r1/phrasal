//
// Stanford Phrasal build specification for
// Gradle.
//
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '3.4.1'

// Gradle application plugin
mainClassName = "edu.stanford.nlp.mt.Phrasal"

// Jar creation
jar {
    manifest {
        attributes 'Implementation-Title': 'Stanford Phrasal',
                   'Implementation-Version': version,
		   'Main-Class': 'edu.stanford.nlp.mt.Phrasal'
    }
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

// TODO(spenceg): Fix gradle build
// Dependency configurations
// 'compile' is the default configuration
//configurations {
//  extra {
//    extendsFrom compile
//  }
//}

// Add src-extra build target
sourceSets {
  main {
    java.srcDirs = ['src/']
    resources.srcDirs = ['src/']
  }
  extra {
    java.srcDirs = ['src-extra/']
    resources.srcDirs = ['src/']
  }
}

repositories {
    mavenCentral()
}

task listDeps << {
    configurations.compile.each { File file -> println file.name }
}

task compileKenLM(type: Exec) {
  commandLine 'src-cc/compile_JNI.sh'
}

dependencies {
  // TERp jars that are no longer available on the web.
  compile fileTree(dir: 'lib', include: '*.jar')
  
  // CoreNLP dependencies
  compile project(":CoreNLP")

  // lilt/core Maven dependencies
  compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
  compile group: 'it.unimi.dsi', name: 'fastutil', version: '6.6.2'
  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.1'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.1'
  testCompile group: 'junit', name: 'junit', version: '4.12'
  compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.2.1.v20140609'
  compile group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '9.2.1.v20140609'
  compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.2.1.v20140609'
  compile group: 'org.eclipse.jetty', name: 'jetty-annotations', version: '9.2.1.v20140609'
  compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.2.1.v20140609'
}

// Eclipse plugin setup
eclipse {
  classpath {
    defaultOutputDir = file('bin/')
    file {
      beforeMerged { classpath ->
        classpath.entries.removeAll { entry -> entry.kind == 'lib' }
      }
    }
  }
}
